
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаписатьИзмененияРаспределенияДокументом(ДокументОбъект) Экспорт
	
	ИнициализироватьЗаписиСуществующегоДокумента(ДокументОбъект);
	
	ИзмененияРаспределения = Неопределено;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказНаВнутреннееПотребление") Тогда
		ИзмененияРаспределения = ИзмененияРаспределенияДокументомЗаказНаВнутреннееПотребление(ДокументОбъект);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИзмененияРаспределения) Тогда
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ИзмененияРаспределения);
		НаборЗаписей.Записать(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СсылкаНаДокумент(ДокументОбъект)
	
	Если НЕ ДокументОбъект.ЭтоНовый() Тогда
		Возврат ДокументОбъект.Ссылка;
	КонецЕсли;
	
	ДокументСсылка = ДокументОбъект.ПолучитьСсылкуНового();
	
	Если НЕ ЗначениеЗаполнено(ДокументСсылка) Тогда
		
		МетаданныеДокумента = ДокументОбъект.Метаданные();
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеДокумента.ПолноеИмя());
		
		ДокументСсылка = МенеджерДокумента.ПолучитьСсылку();
		
		ДокументОбъект.УстановитьСсылкуНового(ДокументСсылка);
		
	КонецЕсли;
	
	Возврат ДокументСсылка;
	
КонецФункции

Процедура ИнициализироватьЗаписиСуществующегоДокумента(ДокументОбъект)
	
	Если ДокументОбъект.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ДокументОбъект.Дата);
	Запрос.УстановитьПараметр("Документ", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Ответственный", Пользователи.ТекущийПользователь());
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИсторияРаспределения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.КСП_ИсторияРаспределенияГотовойПродукции КАК ИсторияРаспределения
	|ГДЕ
	|	ИсторияРаспределения.Документ = &Документ";
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		// Записи уже есть => история по документу уже инициализирована
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказНаВнутреннееПотребление") Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	ДокТовары.Ссылка КАК Документ,
		|	ДокТовары.Номенклатура КАК Номенклатура,
		|	ДокТовары.Характеристика КАК Характеристика,
		|	ДокТовары.Упаковка КАК Упаковка,
		|	ДокТовары.Количество КАК Количество,
		|	&Ответственный КАК Ответственный
		|ИЗ
		|	Документ.ЗаказНаВнутреннееПотребление.Товары КАК ДокТовары
		|ГДЕ
		|	ДокТовары.Ссылка = &Документ";
		
	Иначе
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

Функция ИзмененияРаспределенияДокументомЗаказНаВнутреннееПотребление(ДокументОбъект)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары", ДокументОбъект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ДокументСсылка", СсылкаНаДокумент(ДокументОбъект));
	Запрос.УстановитьПараметр("Ответственный", Пользователи.ТекущийПользователь());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Упаковка КАК Упаковка,
	|	Товары.Количество КАК Количество
	|ПОМЕСТИТЬ втНовыеТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&ДокументСсылка КАК Документ,
	|	Влож.Номенклатура КАК Номенклатура,
	|	Влож.Характеристика КАК Характеристика,
	|	Влож.Упаковка КАК Упаковка,
	|	СУММА(Влож.Количество) КАК Количество,
	|	&Ответственный КАК Ответственный
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокТовары.Номенклатура КАК Номенклатура,
	|		ДокТовары.Характеристика КАК Характеристика,
	|		ДокТовары.Упаковка КАК Упаковка,
	|		-ДокТовары.Количество КАК Количество
	|	ИЗ
	|		Документ.ЗаказНаВнутреннееПотребление.Товары КАК ДокТовары
	|	ГДЕ
	|		ДокТовары.Ссылка = &ДокументСсылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втНовыеТовары.Номенклатура,
	|		втНовыеТовары.Характеристика,
	|		втНовыеТовары.Упаковка,
	|		втНовыеТовары.Количество
	|	ИЗ
	|		втНовыеТовары КАК втНовыеТовары) КАК Влож
	|
	|СГРУППИРОВАТЬ ПО
	|	Влож.Номенклатура,
	|	Влож.Характеристика,
	|	Влож.Упаковка
	|
	|ИМЕЮЩИЕ
	|	СУММА(Влож.Количество) <> 0";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонеЦЕсли