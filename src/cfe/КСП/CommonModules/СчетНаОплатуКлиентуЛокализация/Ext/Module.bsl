
&Вместо("ОбработкаПроведения")
Процедура КСП_ОбработкаПроведения(Объект, Отказ, РежимПроведения)
	
	Движения = Объект.Движения;
	ДополнительныеСвойства = Объект.ДополнительныеСвойства;
	
	// Движения
	// РС КСП_ВыставленныеСчетаПоСтрокамЗаказа
	Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		КСП_ДвиженияРСКСП_ВыставленныеСчетаПоСтрокамЗаказа(Объект, Движения, ДополнительныеСвойства);
	КонецЕсли;
	
КонецПроцедуры

Процедура КСП_ДвиженияРСКСП_ВыставленныеСчетаПоСтрокамЗаказа(Знач Объект, Движения, Знач ДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(&ЗаказКлиента КАК Документ.ЗаказКлиента) КАК Заказ,
		|	&СчетНаОплату КАК Счет,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.КСП_ИдентификаторСтрокиЗаказа КАК ИдентификаторСтрокиЗаказа,
		|	ЗаказКлиентаТовары.Количество КАК Заказано,
		|	ЗаказКлиентаТовары.СуммаСНДС КАК Выставлено // TODO Спросить что за поле
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КСП_ВыставленныеСчетаПоСтрокамЗаказа КАК КСП_ВыставленныеСчетаПоСтрокамЗаказа
		|		ПО ЗаказКлиентаТовары.Ссылка = КСП_ВыставленныеСчетаПоСтрокамЗаказа.Заказ
		|			И ЗаказКлиентаТовары.Номенклатура = КСП_ВыставленныеСчетаПоСтрокамЗаказа.Номенклатура
		|			И ЗаказКлиентаТовары.КСП_ИдентификаторСтрокиЗаказа = КСП_ВыставленныеСчетаПоСтрокамЗаказа.ИдентификаторСтрокиЗаказа
		|			И (КСП_ВыставленныеСчетаПоСтрокамЗаказа.Счет <> &СчетНаОплату)
		|ГДЕ
		|	НЕ ЗаказКлиентаТовары.Ссылка.ПометкаУдаления
		|	И ЗаказКлиентаТовары.Ссылка.Проведен
		|	И ЗаказКлиентаТовары.Ссылка = ВЫРАЗИТЬ(&ЗаказКлиента КАК Документ.ЗаказКлиента)
		|	И ЗаказКлиентаТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)
		|	И КСП_ВыставленныеСчетаПоСтрокамЗаказа.Номенклатура ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ЗаказКлиента", Объект.ДокументОснование);
	Запрос.УстановитьПараметр("СчетНаОплату", Объект.Ссылка);
	
	ВыборкаДвижений = Запрос.Выполнить().Выбрать();
	
	Движения.КСП_ВыставленныеСчетаПоСтрокамЗаказа.Записывать = Истина;
	Пока ВыборкаДвижений.Следующий() Цикл
		Движение = Движения.КСП_ВыставленныеСчетаПоСтрокамЗаказа.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДвижений);
	КонецЦикла;

КонецПроцедуры
