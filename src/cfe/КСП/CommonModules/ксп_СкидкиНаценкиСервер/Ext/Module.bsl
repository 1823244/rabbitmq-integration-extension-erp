// Возввращает сумму заказанных строк по переданной табличной части
//  Товары - Табличная часть
//
// Параметры:
//  Товары	-	Табличная часть 
// 
// Возвращаемое значение:
//  СуммаЗаказанныхСтрок - Число, сумма не отмененных строк по табличной части 
//
Функция ПолучитьСуммуЗаказанныхСтрок(Знач Товары) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Товары.СуммаСНДС КАК СуммаСНДС,
		|	Товары.Отменено КАК Отменено
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(втТовары.СуммаСНДС), 0) КАК СуммаСНДС
		|ИЗ
		|	втТовары КАК втТовары
		|ГДЕ
		|	НЕ втТовары.Отменено");
	
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить(, "СуммаСНДС, Отменено"));
	
	Выгрузка = Запрос.Выполнить().Выгрузить();
	СуммаЗаказанныхСтрок = Выгрузка[0].СуммаСНДС;
	
	Возврат СуммаЗаказанныхСтрок;
	
КонецФункции

// Возвращает сумму оплаченную по заказу по РН РасчетыСКлиентамиПоСрокам
//
// Параметры:
//  Заказ	 - ЗаказКлиентаСсылка	 - Заказ по которому необходимо посчиать оплату
// 
// Возвращаемое значение:
//  Сумма - Число, оплаченная сумма по заказу
//
Функция СуммаОплатПоЗаказу(Знач Заказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(Расчеты.Оплачено) КАК Оплачено
		|ИЗ
		|	(ВЫБРАТЬ
		|		АналитикаУчета.Организация КАК Организация,
		|		АналитикаУчета.Партнер КАК Партнер,
		|		АналитикаУчета.Контрагент КАК Контрагент,
		|		АналитикаУчета.Договор КАК Договор,
		|		АналитикаУчета.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		РасчетыПоСрокам.ОбъектРасчетов КАК ОбъектРасчетов,
		|		ВЫБОР &ДанныеОтчета
		|			КОГДА 4
		|				ТОГДА РасчетыПоСрокам.Валюта
		|			КОГДА 2
		|				ТОГДА &ВалютаУправленческогоУчета
		|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
		|		КОНЕЦ КАК Валюта,
		|		РасчетыПоСрокам.РасчетныйДокумент КАК РасчетныйДокумент,
		|		РасчетыПоСрокам.ДатаВозникновения КАК ДатаВозникновения,
		|		РасчетыПоСрокам.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
		|		РасчетыПоСрокам.ДокументРегистратор КАК Регистратор,
		|		РасчетыПоСрокам.Регистратор КАК РегистраторРасчетов,
		|		РасчетыПоСрокам.Период КАК ПериодСекунда,
		|		ВЫБОР
		|			КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					И НЕ РасчетыПоСрокам.Сторно
		|					И НЕ РасчетыПоСрокам.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиентуНаПлатежнуюКарту), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыНаПлатежнуюКарту), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности))
		|					И НЕ ТИПЗНАЧЕНИЯ(РасчетыПоСрокам.ДокументРегистратор) В (ТИП(Документ.РасчетКурсовыхРазниц), ТИП(Документ.КорректировкаЗадолженности), ТИП(Документ.ВзаимозачетЗадолженности))
		|				ТОГДА ВЫБОР &ДанныеОтчета
		|						КОГДА 4
		|							ТОГДА РасчетыПоСрокам.Предоплата
		|						КОГДА 2
		|							ТОГДА РасчетыПоСрокам.ПредоплатаУпр
		|						ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
		|					КОНЕЦ
		|			КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					И НЕ РасчетыПоСрокам.Сторно
		|					И РасчетыПоСрокам.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиентуНаПлатежнуюКарту), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыНаПлатежнуюКарту), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию))
		|				ТОГДА -ВЫБОР &ДанныеОтчета
		|						КОГДА 4
		|							ТОГДА РасчетыПоСрокам.Долг
		|						КОГДА 2
		|							ТОГДА РасчетыПоСрокам.ДолгУпр
		|						ИНАЧЕ РасчетыПоСрокам.ДолгРегл
		|					КОНЕЦ
		|			КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|					И НЕ РасчетыПоСрокам.Сторно
		|					И НЕ РасчетыПоСрокам.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносЗадолженностиМеждуФилиалами), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаКлиента), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомиссионера), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомитенту))
		|					И НЕ ТИПЗНАЧЕНИЯ(РасчетыПоСрокам.ДокументРегистратор) В (ТИП(Документ.РасчетКурсовыхРазниц), ТИП(Документ.КорректировкаЗадолженности), ТИП(Документ.ВзаимозачетЗадолженности))
		|				ТОГДА ВЫБОР &ДанныеОтчета
		|						КОГДА 4
		|							ТОГДА РасчетыПоСрокам.Долг
		|						КОГДА 2
		|							ТОГДА РасчетыПоСрокам.ДолгУпр
		|						ИНАЧЕ РасчетыПоСрокам.ДолгРегл
		|					КОНЕЦ
		|			КОГДА РасчетыПоСрокам.Сторно
		|					И РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ВЫБОР &ДанныеОтчета
		|						КОГДА 4
		|							ТОГДА РасчетыПоСрокам.Долг
		|						КОГДА 2
		|							ТОГДА РасчетыПоСрокам.ДолгУпр
		|						ИНАЧЕ РасчетыПоСрокам.ДолгРегл
		|					КОНЕЦ
		|			КОГДА РасчетыПоСрокам.Сторно
		|					И РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|					И НЕ РасчетыПоСрокам.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаКлиента))
		|				ТОГДА -ВЫБОР &ДанныеОтчета
		|						КОГДА 4
		|							ТОГДА РасчетыПоСрокам.Предоплата
		|						КОГДА 2
		|							ТОГДА РасчетыПоСрокам.ПредоплатаУпр
		|						ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Оплачено
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
		|			ПО РасчетыПоСрокам.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
		|	ГДЕ
		|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)) КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов.Объект = &ОбъектРасчетов
		|
		|ИМЕЮЩИЕ
		|	СУММА(Расчеты.Оплачено) > 0";
	
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ДанныеОтчета", 4);
	Запрос.УстановитьПараметр("ОбъектРасчетов", Заказ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Сумма = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Сумма = ВыборкаДетальныеЗаписи.Оплачено;
	КонецЦикла;	
	
	Возврат Сумма;
		
КонецФункции