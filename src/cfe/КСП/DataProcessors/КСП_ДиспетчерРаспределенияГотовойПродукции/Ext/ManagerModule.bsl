
#Область СлужебныйПрограммныйИнтерфейс

Функция СписокНастроек(Назначение, Общие = Ложь) Экспорт
	
	ИмяПользователя = Неопределено;
	Если Общие Тогда
		ИмяПользователя = КлючОбъектаНастроек();
	КонецЕсли;
	
	КлючОбъекта = КлючОбъектаНастроек(Назначение);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ХранилищеНастроек().ПолучитьСписок(КлючОбъектаНастроек(Назначение), ИмяПользователя);
	
КонецФункции

Функция ОписаниеНастроек(Назначение, КлючНастроек, Общие = Ложь) Экспорт
	
	ИмяПользователя = Неопределено;
	Если Общие Тогда
		ИмяПользователя = КлючОбъектаНастроек();
	КонецЕсли;
	
	КлючОбъекта = КлючОбъектаНастроек(Назначение);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ХранилищеНастроек().ПолучитьОписание(КлючОбъекта, КлючНастроек, ИмяПользователя);
	
КонецФункции

Функция ЗагрузитьНастройки(ОписаниеНастроек) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ХранилищеНастроек().Загрузить(
		ОписаниеНастроек.КлючОбъекта, ОписаниеНастроек.КлючНастроек,
		ОписаниеНастроек, ОписаниеНастроек.Пользователь);
	
КонецФункции

Функция СохранитьНастройки(Назначение, Настройки, Описание, Знач КлючНастроек = Неопределено, Общие = Ложь) Экспорт
	
	ИмяПользователя = Неопределено;
	Если Общие Тогда
		ИмяПользователя = КлючОбъектаНастроек();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КлючНастроек) Тогда
		КлючНастроек = XMLСтрока(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	КлючОбъекта = КлючОбъектаНастроек(Назначение);
	
	ОписаниеНастроек = Новый ОписаниеНастроек;
	ОписаниеНастроек.Представление	= Описание;
	ОписаниеНастроек.КлючОбъекта	= КлючОбъекта;
	ОписаниеНастроек.КлючНастроек	= КлючНастроек;
	ОписаниеНастроек.Пользователь	= ИмяПользователя;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ХранилищеНастроек().Сохранить(
		КлючОбъекта, КлючНастроек,
		Настройки, ОписаниеНастроек,
		ИмяПользователя);
	
	Возврат ОписаниеНастроек;
	
КонецФункции

Процедура УдалитьНастройки(Назначение, КлючНастроек) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КлючОбъекта = КлючОбъектаНастроек(Назначение);
	
	ХранилищеНастроек().Удалить(КлючОбъекта, КлючНастроек, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КлючОбъектаНастроек(Суффикс = "")
	Возврат Метаданные.Обработки.КСП_ДиспетчерРаспределенияГотовойПродукции.ПолноеИмя() + Суффикс;
КонецФункции

Функция ХранилищеНастроек()
	Возврат ХранилищеПользовательскихНастроекОтчетов;
КонецФункции

#КонецОбласти
