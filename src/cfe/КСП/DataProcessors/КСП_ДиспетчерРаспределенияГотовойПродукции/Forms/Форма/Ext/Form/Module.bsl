
&НаКлиенте
Перем мСтрокаРаспределенияДоИзменения;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаРаспределения) Тогда
		Объект.ДатаРаспределения = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ОбработкаОбъект = ЭтотОбъект();
	
	Объект.АдресИсходныхДанных = ОбработкаОбъект.НовыйАдресИсходныхДанных(УникальныйИдентификатор);
	Объект.АдресОтборовСКД = ОбработкаОбъект.НовыйАдресОтборовСКД(УникальныйИдентификатор);
	ОбработкаОбъект.ИнициализироватьКомпоновщик(Объект.Компоновщик);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	НастройкиОтбора = СохраненныеНастройки("Отборы", НастройкиОтбораКлюч);
	Если ТипЗнч(НастройкиОтбора) = Тип("НастройкиКомпоновкиДанных") Тогда
		Объект.Компоновщик.ЗагрузитьНастройки(НастройкиОтбора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкладОстатковПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.СкладОстатков) Тогда
		Объект.Распределение.ПолучитьЭлементы().Очистить();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКомпоновщикНастройкиЭлементОтбор

&НаКлиенте
Процедура КомпоновщикНастройкиЭлементОтборПриИзменении(Элемент)
	
	ТребуетсяПрименитьОтбор = Истина;
	Элементы.РаспределениеПрименитьОтбор.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаспределение

&НаКлиенте
Процедура РаспределениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные.Уровень = 0
		И Поле = Элементы.РаспределениеНоменклатура Тогда
		ПоказатьЗначение(, ТекущиеДанные.Номенклатура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеПередНачаломИзменения(Элемент, Отказ)
	
	мСтрокаРаспределенияДоИзменения = Новый Структура(
		"КоличествоДоступно,
		|КоличествоДоступноОпт, КоличествоДоступноРозница, КоличествоДоступноECOM");
	ЗаполнитьЗначенияСвойств(мСтрокаРаспределенияДоИзменения, Элементы.Распределение.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеКоличествоДоступноОптПриИзменении(Элемент)
	КоличествоДоступноПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеКоличествоДоступноРозницаПриИзменении(Элемент)
	КоличествоДоступноПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеКоличествоДоступноECOMПриИзменении(Элемент)
	КоличествоДоступноПриИзменении(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	ЗаписатьРаспределение();
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьДанные(Команда)
	
	ЗаполнитьРаспределение();
	
	Элементы.ПанельОсновная.ТекущаяСтраница = Элементы.СтраницаРаспределение;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьОтбор(Команда = Неопределено)
	
	Если НЕ Элементы.ГруппаОтборы.Скрыта() Тогда
		Элементы.ГруппаОтборы.Скрыть();
	КонецЕсли;
	
	ПрименитьОтборНаСервере();
	
	ТребуетсяПрименитьОтбор = Ложь;
	Элементы.РаспределениеЗаписатьИзменения.КнопкаПоУмолчанию = Истина;
	
КонецПроцедуры
&НаСервере
Процедура ПрименитьОтборНаСервере()
	
	ОбработкаОбъект = ЭтотОбъект();
	
	ОбработкаОбъект.ПрименитьНастройкиКомпоновщика();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОтбор(Команда)
	
	ОтключитьЭлементыОтбора(Объект.Компоновщик.Настройки.Отбор);
	
	Для Каждого ГруппировкаНоменклатура Из Объект.Компоновщик.Настройки.Структура Цикл
		ОтключитьЭлементыОтбора(ГруппировкаНоменклатура.Отбор);
		Для Каждого ГруппировкаХарактеристика Из ГруппировкаНоменклатура.Структура Цикл
			ОтключитьЭлементыОтбора(ГруппировкаХарактеристика.Отбор);
		КонецЦикла;
	КонецЦикла;
	
	ПрименитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиОтбора(Команда)
	ИспользоватьНастройкиОтбора(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиОтбора(Команда)
	ИспользоватьНастройкиОтбора(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЭтотОбъект()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаСервере
Процедура ЗаполнитьРаспределение()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаОбъект = ЭтотОбъект();
	
	ОбработкаОбъект.ТекущееРаспределение();
	
	ОбработкаОбъект.ПрименитьНастройкиКомпоновщика(Истина);
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьРаспределение()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаОбъект = ЭтотОбъект();
	Если ОбработкаОбъект.ЗаписатьРаспределение() Тогда
		ЗаполнитьРаспределение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДоступноПриИзменении(ПолеВводаКоличество)
	
	Поля = ИменаСвязанныхПолей(ПолеВводаКоличество);
	
	// Поле "%КоличествоДоступно%" у текущей строки и строки-родителя
	
	ТекущиеДанные = Элементы.Распределение.ТекущиеДанные;
	СтрокаРодитель = ТекущиеДанные.ПолучитьРодителя();
	
	НовоеКоличество = ТекущиеДанные[Поля.Доступно];
	ПредКоличество = мСтрокаРаспределенияДоИзменения[Поля.Доступно];
	
	Дельта = НовоеКоличество - ПредКоличество;
	
	ТекущиеДанные.КоличествоДоступно = ТекущиеДанные.КоличествоДоступно - Дельта;
	СтрокаРодитель[Поля.Доступно] = СтрокаРодитель[Поля.Доступно] + Дельта;
	
	мСтрокаРаспределенияДоИзменения[Поля.Доступно] = НовоеКоличество;
	
	// Поле "%ПроцентОбеспечения%" у текущей строки и строки-родителя
	
	Если ТекущиеДанные[Поля.План] = 0 Тогда
		ТекущиеДанные[Поля.Процент] = 0;
	Иначе
		ТекущиеДанные[Поля.Процент] =
			100 * (ТекущиеДанные[Поля.Распределено] + НовоеКоличество) / ТекущиеДанные[Поля.План];
	КонецЕсли;
	
	Если СтрокаРодитель[Поля.План] = 0 Тогда
		СтрокаРодитель[Поля.Процент] = 0;
	Иначе
		СтрокаРодитель[Поля.Процент] =
			100 * (СтрокаРодитель[Поля.Распределено] + НовоеКоличество) / СтрокаРодитель[Поля.План];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИменаСвязанныхПолей(ПолеВводаКоличество)
	
	ИмяТаблицыФормы = "Распределение";
	
	// Поле "%КоличествоДоступно%"
	ИмяПоляКоличество = СтрЗаменить(ПолеВводаКоличество.Имя, ИмяТаблицыФормы, "");
	
	ИмяТипаПодразделения			= СтрЗаменить(ИмяПоляКоличество, "КоличествоДоступно", "");  // "Опт", "Розница" и т.д.
	ИмяПоляКоличествоПлановое		= "КоличествоПлановое"		+ ИмяТипаПодразделения;
	ИмяПоляКоличествоРаспределено	= "КоличествоРаспределено"	+ ИмяТипаПодразделения;
	ИмяПоляПроцентОбеспечения		= "ПроцентОбеспечения"		+ ИмяТипаПодразделения;
	
	СвязанныеПоля = Новый Структура;
	СвязанныеПоля.Вставить("Доступно",		ИмяПоляКоличество);
	СвязанныеПоля.Вставить("План",			ИмяПоляКоличествоПлановое);
	СвязанныеПоля.Вставить("Распределено",	ИмяПоляКоличествоРаспределено);
	СвязанныеПоля.Вставить("Процент",		ИмяПоляПроцентОбеспечения);
	
	Возврат СвязанныеПоля;
	
КонецФункции

&НаКлиенте
Функция ОтключитьЭлементыОтбора(Отбор)
	
	Для Каждого Элемент Из Отбор.Элементы Цикл
		Элемент.Использование = Ложь;
	КонецЦикла;
	
КонецФункции

#Область УправлениеНастройками

&НаСервере
Функция НастройкиКомпоновщика()
	Возврат Объект.Компоновщик.ПолучитьНастройки();
КонецФункции

&НаСервереБезКонтекста
Функция СохраненныеНастройки(Назначение, КлючНастроек)
	
	МенеджерОбработки = Обработки.КСП_ДиспетчерРаспределенияГотовойПродукции;
	
	// Сначала ищем в личных настройках
	ОписаниеНастроек = МенеджерОбработки.ОписаниеНастроек(Назначение, КлючНастроек);
	
	Если ТипЗнч(ОписаниеНастроек) <> Тип("ОписаниеНастроек") Тогда
		// Если не нашли в личных - ищем в общих настройках
		ОписаниеНастроек = МенеджерОбработки.ОписаниеНастроек(Назначение, КлючНастроек, Истина);
	КонецЕсли;
	
	Настройки = Неопределено;
	Если ТипЗнч(ОписаниеНастроек) = Тип("ОписаниеНастроек") Тогда
		Настройки = МенеджерОбработки.ЗагрузитьНастройки(ОписаниеНастроек);
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

&НаСервере
Функция ИмяФормыНастроек()
	Возврат ЭтотОбъект().Метаданные().ПолноеИмя() + ".Форма.ФормаНастроек";
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуНастроек(Назначение, ИмяМетодаОбработчика, Настройки = Неопределено, Сохранение = Ложь)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Назначение",	Назначение);
	ПараметрыОткрытия.Вставить("Сохранение",	Сохранение);
	
	Если Сохранение Тогда
		ПараметрыОткрытия.Вставить("Настройки",	Настройки);
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения(ИмяМетодаОбработчика, ЭтотОбъект, ПараметрыОткрытия);
	
	ОткрытьФорму(ИмяФормыНастроек(), ПараметрыОткрытия, ЭтотОбъект, , , , ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьНастройкиОтбора(Сохранение)
	
	ОткрытьФормуНастроек(
		"Отборы", "ПриЗакрытииФормыНастроекОтборов",
		НастройкиКомпоновщика(),
		Сохранение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииФормыНастроекОтборов(ОписаниеНастроек, ПараметрыОткрытия) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ОписаниеНастроек) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОтбораКлюч = ОписаниеНастроек.КлючНастроек;
	
	Если НЕ ПараметрыОткрытия.Сохранение Тогда
		Объект.Компоновщик.ЗагрузитьНастройки(ОписаниеНастроек.Настройки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
