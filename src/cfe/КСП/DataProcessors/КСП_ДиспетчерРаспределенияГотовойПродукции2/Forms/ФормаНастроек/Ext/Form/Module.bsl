
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Назначение",	Назначение);
	Параметры.Свойство("Настройки",		Настройки);
	Параметры.Свойство("Сохранение",	Сохранение);
	
	Если НЕ ЗначениеЗаполнено(Настройки) Тогда
		ОписаниеНастроек = ОписаниеНастроек(Назначение);
	КонецЕсли;
	
	Если Сохранение Тогда
		
		Элементы.ФормаСохранитьНастройки.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаЗагрузитьНастройки.Видимость = Ложь;
		
	Иначе
		
		Элементы.ФормаЗагрузитьНастройки.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаСохранитьНастройки.Видимость = Ложь;
		
		Элементы.ОписаниеНастройки.Видимость = Ложь;
		Элементы.ДоступныеНастройкиДобавить.Видимость = Ложь;
		
	КонецЕсли;
	
	ЗаполнитьСписокНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбщиеНастройкиПриИзменении(Элемент)
	
	ЗаполнитьСписокНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДоступныеНастройки

&НаКлиенте
Процедура ДоступныеНастройкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование ИЛИ НЕ Сохранение Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ДоступныеНастройки.ТекущаяСтрока = НоваяСтрокаНастроек().ПолучитьИдентификатор();
	ТекущийЭлемент = Элементы.ОписаниеНастройки;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеНастройкиПередУдалением(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные.Новая Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ПриОтветеНаВопросОбУдалении = Новый ОписаниеОповещения("ПриОтветеНаВопросОбУдалении", ЭтотОбъект, Элемент.ТекущиеДанные);
	ПоказатьВопрос(
		ПриОтветеНаВопросОбУдалении, "Удалить настройку?",
		РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет, , КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеНастройкиПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Новая Тогда
		Элементы.ОписаниеНастройки.Подсказка = "Будет создана новая настройка";
	Иначе
		ОписаниеНастройки = ТекущиеДанные.Описание;
		Элементы.ОписаниеНастройки.Подсказка = "Будет перезаписана выбранная настройка";
	КонецЕсли;
	
	ТекущийЭлемент = Элементы.ОписаниеНастройки;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеНастройкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Сохранение Тогда
		СохранитьНастройки();
	Иначе
		ЗагрузитьНастройки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройки(Команда = Неопределено)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДоступныеНастройки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные = НоваяСтрокаНастроек();
	КонецЕсли;
	
	КлючНастроек = СохранитьНастройкиНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	
	Закрыть(ОписаниеНастроек(КлючНастроек));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройки(Команда = Неопределено)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДоступныеНастройки.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = ЗагрузитьНастройкиНаСервере(ТекущиеДанные.ПолучитьИдентификатор());
	
	Закрыть(ОписаниеНастроек(ТекущиеДанные.КлючНастроек, Настройки));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция НоваяСтрокаНастроек()
	
	НовыеСтроки = ДоступныеНастройки.НайтиСтроки(Новый Структура("Новая", Истина));
	
	Если ЗначениеЗаполнено(НовыеСтроки) Тогда
		НоваяСтрока = НовыеСтроки[0];
	Иначе
		НоваяСтрока = ДоступныеНастройки.Добавить();
		НоваяСтрока.Новая = Истина;
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокНастроек()
	
	ДоступныеНастройки.Очистить();
	ОписаниеНастройки = "";
	Элементы.ОписаниеНастройки.Подсказка = "";
	
	СписокНастроек = МенеджерНастроек().СписокНастроек(Назначение, ОбщиеНастройки);
	Для Каждого Элемент Из СписокНастроек Цикл
		
		НовСтр = ДоступныеНастройки.Добавить();
		НовСтр.КлючНастроек = Элемент.Значение;
		НовСтр.Описание = Элемент.Представление;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДоступныеНастройки) Тогда
		Элементы.ДоступныеНастройки.ТекущаяСтрока = ДоступныеНастройки[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОтветеНаВопросОбУдалении(РезультатВопроса, ТекущиеДанные) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьНастройкиНаСервере(Назначение, ТекущиеДанные.КлючНастроек);
		ДоступныеНастройки.Удалить(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьНастройкиНаСервере(Назначение, КлючНастроек)
	МенеджерНастроек().УдалитьНастройки(Назначение, КлючНастроек);
КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиНаСервере(ИдСтроки)
	
	СтрокаНастройки = ДоступныеНастройки.НайтиПоИдентификатору(ИдСтроки);
	
	КлючНастроек = ?(СтрокаНастройки.Новая, Неопределено, СтрокаНастройки.КлючНастроек);
	
	ОписаниеНастроек = МенеджерНастроек().СохранитьНастройки(
		Назначение,
		Настройки, ОписаниеНастройки,
		КлючНастроек, ОбщиеНастройки);
	
	Возврат ОписаниеНастроек.КлючНастроек;
	
КонецФункции

&НаСервере
Функция ЗагрузитьНастройкиНаСервере(ИдСтроки)
	
	СтрокаНастройки = ДоступныеНастройки.НайтиПоИдентификатору(ИдСтроки);
	
	ОписаниеНастроек = МенеджерНастроек().ОписаниеНастроек(
		Назначение, СтрокаНастройки.КлючНастроек, ОбщиеНастройки);
	
	Возврат МенеджерНастроек().ЗагрузитьНастройки(ОписаниеНастроек);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеНастроек(КлючНастроек, Настройки = Неопределено)
	
	Возврат Новый Структура(
		"КлючНастроек, Настройки",
		КлючНастроек, Настройки);
	
КонецФункции

&НаСервереБезКонтекста
Функция МенеджерНастроек()
	Возврат Обработки.КСП_ДиспетчерРаспределенияГотовойПродукции;
КонецФункции

#КонецОбласти
