
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаТабличнойЧасти Из ДвиженияКодовМаркировки Цикл
		
		Если СтрокаТабличнойЧасти.Номенклатура.ВестиУчетПоХарактеристикам 
			И Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры) Тогда
			ТекстСообщения = СтрШаблон("Не заполнена колонка ""Характеристика номенклатуры"" в строке %1 списка ""Движения кодов маркировки""", СтрокаТабличнойЧасти.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЕсли;	
		
		Если Не СтрокаТабличнойЧасти.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС
			И Не СтрокаТабличнойЧасти.СтавкаНДС = Перечисления.СтавкиНДС.НДС0
			И СтрокаТабличнойЧасти.СуммаНДС = 0 Тогда
			ТекстСообщения = СтрШаблон("Не заполнена колонка ""Сумма НДС"" в строке %1 списка ""Движения кодов маркировки""", СтрокаТабличнойЧасти.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	МассивПроверяемыхРеквизитов = Новый Массив;
	МассивПроверяемыхРеквизитов.Добавить("Организация");
	МассивПроверяемыхРеквизитов.Добавить("ИмяФайла");
	МассивПроверяемыхРеквизитов.Добавить("ВидОперации");
	МассивПроверяемыхРеквизитов.Добавить("ВидПродукции");
	МассивПроверяемыхРеквизитов.Добавить("Заказ");
	МассивПроверяемыхРеквизитов.Добавить("НомерЗаказа");
	МассивПроверяемыхРеквизитов.Добавить("ДатаЗаказа");
	
	Для Каждого ИмяПроверяемогоРеквизита Из МассивПроверяемыхРеквизитов Цикл
		Если Не ЗначениеЗаполнено(ЭтотОбъект[ИмяПроверяемогоРеквизита]) Тогда
			ЗагруженСОшибками = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
	Если ДвиженияКодовМаркировки.Количество() = 0 Тогда
		ЗагруженСОшибками = Истина;
	КонецЕсли;	
	
	МассивПроверяемыхРеквизитовТЧ = Новый Массив;
	МассивПроверяемыхРеквизитовТЧ.Добавить("КодМаркировки");
	МассивПроверяемыхРеквизитовТЧ.Добавить("Номенклатура");
	МассивПроверяемыхРеквизитовТЧ.Добавить("ХарактеристикаНоменклатуры");
	МассивПроверяемыхРеквизитовТЧ.Добавить("Количество");
	МассивПроверяемыхРеквизитовТЧ.Добавить("Цена");
	МассивПроверяемыхРеквизитовТЧ.Добавить("Сумма");
	МассивПроверяемыхРеквизитовТЧ.Добавить("СтавкаНДС");
	МассивПроверяемыхРеквизитовТЧ.Добавить("СуммаСНДС");

	Для Каждого СтрокаТЧ Из ДвиженияКодовМаркировки Цикл
		Для Каждого ИмяПроверяемогоРеквизита Из МассивПроверяемыхРеквизитовТЧ Цикл
			Если Не ЗначениеЗаполнено(СтрокаТЧ[ИмяПроверяемогоРеквизита]) Тогда
				ЗагруженСОшибками = Истина;
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры
