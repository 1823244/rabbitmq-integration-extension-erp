// +++ УваровДС 14/12/2023
// заново добавил кнопку печать

#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
&После("ДобавитьКомандыПечати")
Процедура КСП_ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Декларация
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ТаможеннаяДекларацияЭкспорт";
	КомандаПечати.Идентификатор = "Декларация";                     
	//КомандаПечати.МестоРазмещения = "<ИмяПодменюНаФорме>";
	КомандаПечати.Представление = НСтр("ru = 'Декларация о происхождении товара'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Выписка из прайс-листа продукции
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ТаможеннаяДекларацияЭкспорт";
	КомандаПечати.Идентификатор = "ВыпискаИзПрайсЛиста";                     
	//КомандаПечати.МестоРазмещения = "<ИмяПодменюНаФорме>";
	КомандаПечати.Представление = НСтр("ru = 'Выписка из прайс-листа продукции'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Реализация товаров штрих-коды
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ТаможеннаяДекларацияЭкспорт";
	КомандаПечати.Идентификатор = "РеализацияТоваровШтрихКоды";                     
	//КомандаПечати.МестоРазмещения = "<ИмяПодменюНаФорме>";
	КомандаПечати.Представление = НСтр("ru = 'Реализация товаров штрих-коды'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Реализация товаров
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ТаможеннаяДекларацияЭкспорт";
	КомандаПечати.Идентификатор = "РеализацияТоваров";                     
	//КомандаПечати.МестоРазмещения = "<ИмяПодменюНаФорме>";
	КомандаПечати.Представление = НСтр("ru = 'Реализация товаров'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Спецификация
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ТаможеннаяДекларацияЭкспорт";
	КомандаПечати.Идентификатор = "Спецификация";                     
	//КомандаПечати.МестоРазмещения = "<ИмяПодменюНаФорме>";
	КомандаПечати.Представление = НСтр("ru = 'Спецификация'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

#КонецОбласти


// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов – Массив – ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати – Структура – дополнительные настройки печати;
//  КоллекцияПечатныхФорм – ТаблицаЗначений – сформированные табличные документы (выходной параметр)
//  ОбъектыПечати – СписокЗначений – значение – ссылка на объект;
//                                            представление – имя области, в которой был выведен объект (выходной параметр);
//  ПараметрыВывода – Структура – дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Декларация") Тогда	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Декларация",
		НСтр("ru = 'КСП ПФ MXL Декларация'"),
		ПечатьДекларация(МассивОбъектов, ОбъектыПечати), ,
		"Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_Декларация");	
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВыпискаИзПрайсЛиста") Тогда	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ВыпискаИзПрайсЛиста",
		НСтр("ru = 'КСП ПФ MXL Выписка из прайс листа'"),
		ПечатьВыпискаИзПрайсЛиста(МассивОбъектов, ОбъектыПечати), ,
		"Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_ВыпискаИзПрайсЛиста");	
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеализацияТоваровШтрихКоды") Тогда	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РеализацияТоваровШтрихКоды",
		НСтр("ru = 'КСП ПФ MXL Реализация товаров штрих коды'"),
		ПечатьРеализацияТоваровШтрихКоды(МассивОбъектов, ОбъектыПечати), ,
		"Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_РеализацияТоваровШтрихКоды");	
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеализацияТоваров") Тогда	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"РеализацияТоваров",
		НСтр("ru = 'КСП ПФ MXL Реализация товаров'"),
		ПечатьРеализацияТоваров(МассивОбъектов, ОбъектыПечати), ,
		"Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_РеализацияТоваров");	
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Спецификация") Тогда	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Спецификация",
		НСтр("ru = 'КСП ПФ MXL Спецификация'"),
		ПечатьСпецификация(МассивОбъектов, ОбъектыПечати), ,
		"Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_Спецификация");	
	КонецЕсли;
	
КонецПроцедуры      

Функция ПечатьДекларация(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати.
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Декларация";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_Декларация");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка2 = Макет.ПолучитьОбласть("Шапка2");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьРеквизиты = Макет.ПолучитьОбласть("Реквизиты");
	
	ОбластьНаименование = Макет.ПолучитьОбласть("Наименование");
	
	ОбластьФутер = Макет.ПолучитьОбласть("Футер");
	
	ДокументПередачаТоваров = МассивОбъектов[0].ДокументОснование;
	
	// Получаем запросом необходимые данные.
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.НаименованиеПолное КАК НаименованиеПолное,
	|	Организации.КПП КАК КПП,
	|	Организации.ИНН КАК ИНН,
	|	Организации.ОГРН КАК ОГРН,
	|	Организации.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	БанковскиеСчетаОрганизаций.НомерСчета КАК НомерСчета,
	|	БанковскиеСчетаОрганизаций.КоррСчетБанка КАК КоррСчетБанка,
	|	БанковскиеСчетаОрганизаций.Банк КАК Банк,
	|	БанковскиеСчетаОрганизаций.БИКБанка КАК БИКБанка,
	|	БанковскиеСчетаОрганизаций.Ссылка КАК СчетСсылка
	|ПОМЕСТИТЬ втОргИБанкЭлис
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|		ПО (Организации.Ссылка = БанковскиеСчетаОрганизаций.Владелец)
	|ГДЕ
	|	Организации.ИНН = &ИННЭлис
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииКонтактнаяИнформация.Тип КАК Тип,
	|	ОрганизацииКонтактнаяИнформация.Представление КАК Представление,
	|	ОрганизацииКонтактнаяИнформация.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втКонт
	|ИЗ
	|	втОргИБанкЭлис КАК втОргИБанкЭлис
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|		ПО втОргИБанкЭлис.Ссылка = ОрганизацииКонтактнаяИнформация.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОргИБанкЭлис.Ссылка КАК Ссылка,
	|	втОргИБанкЭлис.НаименованиеПолное КАК ПолноеЮрНаименование,
	|	втОргИБанкЭлис.КПП КАК КПП,
	|	втОргИБанкЭлис.ИНН КАК ИНН,
	|	втОргИБанкЭлис.ОГРН КАК ОГРН,
	|	втОргИБанкЭлис.НаименованиеСокращенное КАК СокрЮрНаименование,
	|	втОргИБанкЭлис.СчетСсылка КАК СчетСсылка,
	|	втОргИБанкЭлис.СчетСсылка.Представление КАК СчетПредставление,
	|	втОргИБанкЭлис.НомерСчета КАК РасчСчет,
	|	втОргИБанкЭлис.КоррСчетБанка КАК КоррСчет,
	|	втОргИБанкЭлис.Банк.Наименование КАК НаименованиеБанка,
	|	втОргИБанкЭлис.БИКБанка КАК БИК,
	|	Адрес.Представление КАК Адрес,
	|	ВебСтраница.Представление КАК ВебСтраница,
	|	Телефон.Представление КАК Телефон,
	|	Факс.Представление КАК Факс,
	|	ЭлПочта.Представление КАК ЭлПочта
	|ПОМЕСТИТЬ втШапка
	|ИЗ
	|	втОргИБанкЭлис КАК втОргИБанкЭлис
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК Телефон
	|		ПО втОргИБанкЭлис.Ссылка = Телефон.Ссылка
	|			И (Телефон.Тип = &Телефон)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК Адрес
	|		ПО втОргИБанкЭлис.Ссылка = Адрес.Ссылка
	|			И (Адрес.Тип = &Адрес)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК Факс
	|		ПО втОргИБанкЭлис.Ссылка = Факс.Ссылка
	|			И (Факс.Тип = &Факс)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК ВебСтраница
	|		ПО втОргИБанкЭлис.Ссылка = ВебСтраница.Ссылка
	|			И (ВебСтраница.Тип = &ВебСтраница)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК ЭлПочта
	|		ПО втОргИБанкЭлис.Ссылка = ЭлПочта.Ссылка
	|			И (ЭлПочта.Тип = &ЭлПочта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииПрисоединенныеФайлы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втЛоготипОрганизации
	|ИЗ
	|	втОргИБанкЭлис КАК втОргИБанкЭлис
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОрганизацииПрисоединенныеФайлы КАК ОрганизацииПрисоединенныеФайлы
	|		ПО втОргИБанкЭлис.Ссылка = ОрганизацииПрисоединенныеФайлы.ВладелецФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаможеннаяДекларацияЭкспорт.Ссылка КАК Ссылка,
	|	ТаможеннаяДекларацияЭкспорт.Дата КАК Дата,
	|	ТаможеннаяДекларацияЭкспорт.КСП_ТаможенныйОрган КАК КСП_ТаможенныйОрган
	|ПОМЕСТИТЬ втТаможенная
	|ИЗ
	|	Документ.ТаможеннаяДекларацияЭкспорт КАК ТаможеннаяДекларацияЭкспорт
	|ГДЕ
	|	ТаможеннаяДекларацияЭкспорт.Ссылка = &СсылкаТаможенная
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация КАК Грузоотправитель,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.НаименованиеСокращенное КАК ГрузоотправительНаименование,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.ИНН КАК ГрузоотправительИНН,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.КПП КАК ГрузоотправительКПП,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.ОГРН КАК ГрузоотправительОГРН,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель КАК Получатель,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.НаименованиеСокращенное КАК ПолучательНаименование,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.ИНН КАК ПолучательИНН,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.КПП КАК ПолучательКПП,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.ОГРН КАК ПолучательОГРН,
	|	ОрганизацияОтпрКонт.Представление КАК ГрузоотправительАдреc,
	|	ОрганизацияПолКонт.Представление КАК ПолучательАдрес,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка КАК ТаможеннаяСсылка
	|ПОМЕСТИТЬ втПередачи
	|ИЗ
	|	Документ.ТаможеннаяДекларацияЭкспорт.ДокументыОснования КАК ТаможеннаяДекларацияЭкспортДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацияПолКонт
	|		ПО (ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель = ОрганизацияПолКонт.Ссылка)
	|			И (ОрганизацияПолКонт.Тип = &Адрес)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацияОтпрКонт
	|		ПО (ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация = ОрганизацияОтпрКонт.Ссылка)
	|			И (ОрганизацияОтпрКонт.Тип = &Адрес)
	|ГДЕ
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка В
	|			(ВЫБРАТЬ
	|				втТаможенная.Ссылка КАК Ссылка
	|			ИЗ
	|				втТаможенная КАК втТаможенная)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетФактураВыданный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураВыданный.Ссылка КАК Ссылка,
	|	СчетФактураВыданный.Номер КАК Номер,
	|	СчетФактураВыданный.Дата КАК Дата
	|ПОМЕСТИТЬ втСчетФактуры
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|ГДЕ
	|	СчетФактураВыданный.ДокументОснование В
	|			(ВЫБРАТЬ
	|				втПередачи.ДокументОснование
	|			ИЗ
	|				втПередачи КАК втПередачи)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПередачи.ДокументОснование КАК ДокументОснование,
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура КАК Номенклатура,
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура.ВидНоменклатуры КАК НоменклатураВидНоменклатуры,
	|	СУММА(ПередачаТоваровМеждуОрганизациямиТовары.Количество) КАК Количество,
	|	СУММА(ПередачаТоваровМеждуОрганизациямиТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ПередачаТоваровМеждуОрганизациямиТовары.СуммаСНДС) КАК СуммаСНДС
	|ПОМЕСТИТЬ втПердачиТовары
	|ИЗ
	|	втПередачи КАК втПередачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ПередачаТоваровМеждуОрганизациямиТовары
	|		ПО втПередачи.ДокументОснование = ПередачаТоваровМеждуОрганизациямиТовары.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура.ВидНоменклатуры,
	|	втПередачи.ДокументОснование,
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСчетФактуры.Номер КАК Номер,
	|	втСчетФактуры.Дата КАК Дата,
	|	втПердачиТовары.Количество КАК Количество,
	|	втПердачиТовары.СуммаСНДС КАК Сумма
	|ПОМЕСТИТЬ втВыводСтрокиСФ
	|ИЗ
	|	втПердачиТовары КАК втПердачиТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетФактуры КАК втСчетФактуры
	|		ПО втПердачиТовары.ДокументОснование = втСчетФактуры.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПердачиТовары.НоменклатураВидНоменклатуры КАК ВидНоменклатуры,
	|	СУММА(втПердачиТовары.Количество) КАК Кол
	|ПОМЕСТИТЬ втВыводСтрокиВидНоменклатуры
	|ИЗ
	|	втПердачиТовары КАК втПердачиТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	втПердачиТовары.НоменклатураВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПередачи.ТаможеннаяСсылка КАК ТаможеннаяСсылка,
	|	втПередачи.ДокументОснование КАК ДокументОснование,
	|	втПередачи.ТаможеннаяСсылка.Дата КАК Дата,
	|	втПередачи.ТаможеннаяСсылка.КСП_ТаможенныйОрган КАК ТаможенныйОрган,
	|	втПередачи.Грузоотправитель.СтранаПостоянногоМестонахождения.Представление КАК СтранаПроисхождения,
	|	втПередачи.ДокументОснование.Автор.Представление КАК НачальникСклада
	|ПОМЕСТИТЬ втДопДанные
	|ИЗ
	|	втПередачи КАК втПередачи";
	
	Запрос.УстановитьПараметр("СсылкаТаможенная", МассивОбъектов[0]);
	Запрос.УстановитьПараметр("ИННЭлис", "6152001000");
	Запрос.УстановитьПараметр("Телефон", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Адрес", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Факс", Перечисления.ТипыКонтактнойИнформации.Факс);
	Запрос.УстановитьПараметр("ВебСтраница", Перечисления.ТипыКонтактнойИнформации.ВебСтраница);
	Запрос.УстановитьПараметр("ЭлПочта", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	ДопДанныеТЗ = РезультатыЗапросов[10].Выгрузить();
	
	Для Каждого СтрокаТЗ ИЗ ДопДанныеТЗ Цикл	
		Если СтрокаТЗ.ДокументОснование <> Неопределено Тогда 
			ОбластьРеквизиты.Параметры.ТаможенныйОрган = СтрокаТЗ.ТаможенныйОрган;
			ОбластьФутер.Параметры.Дата = СтрокаТЗ.Дата;
			ОбластьФутер.Параметры.СтранаПроисхождения = СтрокаТЗ.СтранаПроисхождения;
			ОбластьФутер.Параметры.НачальникСклада = СтрокаТЗ.НачальникСклада;	
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	
	
	ФайлХранилищеЛого = Неопределено; 
	
	Шапка = РезультатыЗапросов[2].Выбрать();
	Если РезультатыЗапросов[3].Пустой() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Логотип организации не установлен!";
		Сообщение.Сообщить();                                 
	Иначе
		ЛоготипВыборка = РезультатыЗапросов[3].Выбрать();
		Если ЛоготипВыборка.Следующий() Тогда
			ФайлХранилищеЛого = Новый Картинка(РаботаСФайлами.ДвоичныеДанныеФайла(ЛоготипВыборка.Ссылка), Ложь);	
		КонецЕсли;	
	КонецЕсли;
	
	Пока Шапка.Следующий() Цикл
		
		//Начало вывода документа
		// склонение банка
		ФорматнаяСтрока = "ПД=Предложный";
		РезультатСколнения = ПолучитьСклоненияСтроки(Шапка.НаименованиеБанка, ,ФорматнаяСтрока);
		
		ОбластьШапка.Параметры.Заполнить(Шапка);
		ОбластьШапка.Параметры.Логотип = ФайлХранилищеЛого;
		
		ОбластьШапка.Параметры.НаименованиеБанка = РезультатСколнения[0];
		ОбластьШапка2.Параметры.Заполнить(Шапка);
		
		ОбластьРеквизиты.Параметры.Заполнить(Шапка);
		//Конец вывода документа
		
	КонецЦикла;
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ТабличныйДокумент.Вывести(ОбластьШапка2);
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	РеквизитыВыборка = РезультатыЗапросов[5].Выбрать();
	
	Пока РеквизитыВыборка.Следующий() Цикл
		
		ОбластьРеквизиты.Параметры.Заполнить(РеквизитыВыборка);	
		
	КонецЦикла;
	
	ТабличныйДокумент.Вывести(ОбластьРеквизиты);
	
	СтрокаСФВыборка = РезультатыЗапросов[8].Выбрать();
	
	Пока СтрокаСФВыборка.Следующий() Цикл
		
		ОбластьСтрокаСФ = Макет.ПолучитьОбласть("СтрокаСФ");
		ОбластьСтрокаСФ.Параметры.Заполнить(СтрокаСФВыборка);	
		ТабличныйДокумент.Вывести(ОбластьСтрокаСФ);
		
	КонецЦикла;
	
	
	ТабличныйДокумент.Вывести(ОбластьНаименование);
	
	СтрокаВидВыборка = РезультатыЗапросов[9].Выбрать();
	
	Пока СтрокаВидВыборка.Следующий() Цикл
		
		ОбластьСтрокаНаименование = Макет.ПолучитьОбласть("СтрокаНаименование");
		ОбластьСтрокаНаименование.Параметры.Заполнить(СтрокаВидВыборка);	
		ТабличныйДокумент.Вывести(ОбластьСтрокаНаименование);
		
	КонецЦикла;
	
	
	ТабличныйДокумент.Вывести(ОбластьФутер);	
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

Функция ПечатьВыпискаИзПрайсЛиста(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати.
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ВыпискаИзПрайсЛиста";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_ВыпискаИзПрайсЛиста");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка2 = Макет.ПолучитьОбласть("Шапка2");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.НаименованиеПолное КАК НаименованиеПолное,
	|	Организации.КПП КАК КПП,
	|	Организации.ИНН КАК ИНН,
	|	Организации.ОГРН КАК ОГРН,
	|	Организации.НаименованиеСокращенное КАК НаименованиеСокращенное,
	|	БанковскиеСчетаОрганизаций.НомерСчета КАК НомерСчета,
	|	БанковскиеСчетаОрганизаций.КоррСчетБанка КАК КоррСчетБанка,
	|	БанковскиеСчетаОрганизаций.Банк КАК Банк,
	|	БанковскиеСчетаОрганизаций.БИКБанка КАК БИКБанка,
	|	БанковскиеСчетаОрганизаций.Ссылка КАК СчетСсылка
	|ПОМЕСТИТЬ втОргИБанкЭлис
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|		ПО (Организации.Ссылка = БанковскиеСчетаОрганизаций.Владелец)
	|ГДЕ
	|	Организации.ИНН = &ИННЭлис
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииКонтактнаяИнформация.Тип КАК Тип,
	|	ОрганизацииКонтактнаяИнформация.Представление КАК Представление,
	|	ОрганизацииКонтактнаяИнформация.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втКонт
	|ИЗ
	|	втОргИБанкЭлис КАК втОргИБанкЭлис
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|		ПО втОргИБанкЭлис.Ссылка = ОрганизацииКонтактнаяИнформация.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОргИБанкЭлис.Ссылка КАК Ссылка,
	|	втОргИБанкЭлис.НаименованиеПолное КАК ПолноеЮрНаименование,
	|	втОргИБанкЭлис.КПП КАК КПП,
	|	втОргИБанкЭлис.ИНН КАК ИНН,
	|	втОргИБанкЭлис.ОГРН КАК ОГРН,
	|	втОргИБанкЭлис.НаименованиеСокращенное КАК СокрЮрНаименование,
	|	втОргИБанкЭлис.СчетСсылка КАК СчетСсылка,
	|	втОргИБанкЭлис.СчетСсылка.Представление КАК СчетПредставление,
	|	втОргИБанкЭлис.НомерСчета КАК РасчСчет,
	|	втОргИБанкЭлис.КоррСчетБанка КАК КоррСчет,
	|	втОргИБанкЭлис.Банк.Наименование КАК НаименованиеБанка,
	|	втОргИБанкЭлис.БИКБанка КАК БИК,
	|	Адрес.Представление КАК Адрес,
	|	ВебСтраница.Представление КАК ВебСтраница,
	|	Телефон.Представление КАК Телефон,
	|	Факс.Представление КАК Факс,
	|	ЭлПочта.Представление КАК ЭлПочта
	|ПОМЕСТИТЬ втШапка
	|ИЗ
	|	втОргИБанкЭлис КАК втОргИБанкЭлис
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК Телефон
	|		ПО втОргИБанкЭлис.Ссылка = Телефон.Ссылка
	|			И (Телефон.Тип = &Телефон)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК Адрес
	|		ПО втОргИБанкЭлис.Ссылка = Адрес.Ссылка
	|			И (Адрес.Тип = &Адрес)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК Факс
	|		ПО втОргИБанкЭлис.Ссылка = Факс.Ссылка
	|			И (Факс.Тип = &Факс)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК ВебСтраница
	|		ПО втОргИБанкЭлис.Ссылка = ВебСтраница.Ссылка
	|			И (ВебСтраница.Тип = &ВебСтраница)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКонт КАК ЭлПочта
	|		ПО втОргИБанкЭлис.Ссылка = ЭлПочта.Ссылка
	|			И (ЭлПочта.Тип = &ЭлПочта)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииПрисоединенныеФайлы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ втЛоготипОрганизации
	|ИЗ
	|	втОргИБанкЭлис КАК втОргИБанкЭлис
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОрганизацииПрисоединенныеФайлы КАК ОрганизацииПрисоединенныеФайлы
	|		ПО втОргИБанкЭлис.Ссылка = ОрганизацииПрисоединенныеФайлы.ВладелецФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаможеннаяДекларацияЭкспорт.Ссылка КАК Ссылка,
	|	ТаможеннаяДекларацияЭкспорт.Дата КАК Дата,
	|	ТаможеннаяДекларацияЭкспорт.КСП_ТаможенныйОрган КАК КСП_ТаможенныйОрган
	|ПОМЕСТИТЬ втТаможенная
	|ИЗ
	|	Документ.ТаможеннаяДекларацияЭкспорт КАК ТаможеннаяДекларацияЭкспорт
	|ГДЕ
	|	ТаможеннаяДекларацияЭкспорт.Ссылка = &СсылкаТаможенная
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация КАК Грузоотправитель,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.НаименованиеСокращенное КАК ГрузоотправительНаименование,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.ИНН КАК ГрузоотправительИНН,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.КПП КАК ГрузоотправительКПП,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация.ОГРН КАК ГрузоотправительОГРН,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель КАК Получатель,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.НаименованиеСокращенное КАК ПолучательНаименование,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.ИНН КАК ПолучательИНН,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.КПП КАК ПолучательКПП,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель.ОГРН КАК ПолучательОГРН,
	|	ОрганизацияОтпрКонт.Представление КАК ГрузоотправительАдреc,
	|	ОрганизацияПолКонт.Представление КАК ПолучательАдрес,
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка КАК ТаможеннаяСсылка
	|ПОМЕСТИТЬ втПередачи
	|ИЗ
	|	Документ.ТаможеннаяДекларацияЭкспорт.ДокументыОснования КАК ТаможеннаяДекларацияЭкспортДокументыОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацияПолКонт
	|		ПО ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.ОрганизацияПолучатель = ОрганизацияПолКонт.Ссылка
	|			И (ОрганизацияПолКонт.Тип = &Адрес)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацияОтпрКонт
	|		ПО ТаможеннаяДекларацияЭкспортДокументыОснования.ДокументОснование.Организация = ОрганизацияОтпрКонт.Ссылка
	|			И (ОрганизацияОтпрКонт.Тип = &Адрес)
	|ГДЕ
	|	ТаможеннаяДекларацияЭкспортДокументыОснования.Ссылка В
	|			(ВЫБРАТЬ
	|				втТаможенная.Ссылка КАК Ссылка
	|			ИЗ
	|				втТаможенная КАК втТаможенная)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ втПередачиТовары
	|ИЗ
	|	втПередачи КАК втПередачи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ПередачаТоваровМеждуОрганизациямиТовары
	|		ПО втПередачи.ДокументОснование = ПередачаТоваровМеждуОрганизациямиТовары.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АВТОНОМЕРЗАПИСИ() КАК НомерСтр,
	|	ЦеныНоменклатуры25СрезПоследних.Номенклатура.Представление КАК Наименование,
	|	ЦеныНоменклатуры25СрезПоследних.Цена КАК Цена
	|ПОМЕСТИТЬ втЦены
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(
	|			&Дата,
	|			Номенклатура В (ВЫБРАТЬ втТов.Номенклатура ИЗ втПередачиТовары КАК втТов)
	|			И ВидЦены = &ВидЦены) КАК ЦеныНоменклатуры25СрезПоследних";
	
	Запрос.УстановитьПараметр("СсылкаТаможенная", МассивОбъектов[0]);
	Запрос.УстановитьПараметр("ИННЭлис", "6152001000");
	Запрос.УстановитьПараметр("Телефон", Перечисления.ТипыКонтактнойИнформации.Телефон);
	Запрос.УстановитьПараметр("Адрес", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("Факс", Перечисления.ТипыКонтактнойИнформации.Факс);
	Запрос.УстановитьПараметр("ВебСтраница", Перечисления.ТипыКонтактнойИнформации.ВебСтраница);
	Запрос.УстановитьПараметр("ЭлПочта", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
	Запрос.УстановитьПараметр("ВидЦены", Справочники.ВидыЦен.НайтиПоНаименованию("Цена розницы Основная"));
	Запрос.УстановитьПараметр("Дата", КонецМесяца(МассивОбъектов[0].Дата));
	
	РезультатыЗапросов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();	
	
	ФайлХранилищеЛого = Неопределено; 
	
	Шапка = РезультатыЗапросов[2].Выбрать();
	Если РезультатыЗапросов[3].Пустой() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Логотип организации не установлен!";
		Сообщение.Сообщить();                                 
	Иначе
		ЛоготипВыборка = РезультатыЗапросов[3].Выбрать();
		Если ЛоготипВыборка.Следующий() Тогда
			ФайлХранилищеЛого = Новый Картинка(РаботаСФайлами.ДвоичныеДанныеФайла(ЛоготипВыборка.Ссылка), Ложь);	
		КонецЕсли;	
	КонецЕсли;
	
	Пока Шапка.Следующий() Цикл
		
		//Начало вывода документа
		// склонение банка
		ФорматнаяСтрока = "ПД=Предложный";
		РезультатСколнения = ПолучитьСклоненияСтроки(Шапка.НаименованиеБанка, ,ФорматнаяСтрока);
		
		ОбластьШапка.Параметры.Заполнить(Шапка);
		ОбластьШапка.Параметры.Логотип = ФайлХранилищеЛого;
		
		ОбластьШапка.Параметры.НаименованиеБанка = РезультатСколнения[0];
		ОбластьШапка2.Параметры.Заполнить(Шапка);
		ОбластьШапка2.Параметры.Дата = Формат(КонецДня(МассивОбъектов[0].Дата), "ДФ=dd.MM.yyyy");
		
		//Конец вывода документа
		
	КонецЦикла;
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ТабличныйДокумент.Вывести(ОбластьШапка2);
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ЦеныВыборка = РезультатыЗапросов[7].Выбрать();
	
	Пока ЦеныВыборка.Следующий() Цикл
		
		ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
		ОбластьСтрокаТаблицы.Параметры.Заполнить(ЦеныВыборка);
		ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

Функция ПечатьРеализацияТоваровШтрихКоды(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати.
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РеализацияТоваровШтрихКоды";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_РеализацияТоваровШтрихКоды");
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

Функция ПечатьРеализацияТоваров(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати.
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_РеализацияТоваров";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_РеализацияТоваров");
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

Функция ПечатьСпецификация(МассивОбъектов, ОбъектыПечати)
	
	// Создаем табличный документ и устанавливаем имя параметров печати.
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Спецификация";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТаможеннаяДекларацияЭкспорт.КСП_ПФ_MXL_Спецификация");
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Возврат ТабличныйДокумент;
	
КонецФункции 



// --- УваровДС
