#Область КСП_ОбработчикиСобытийФормы

&НаСервере
&После("ПриЧтенииСозданииНаСервере")
Процедура КСП_ПриЧтенииСозданииНаСервере()
	
	КСП_ЗаполнитьСуммуПланаПродаж();
	
КонецПроцедуры

#КонецОбласти

#Область КСП_СлужебныеПроцедурыИФункции

&НаСервере
&После("ПартнерПриИзмененииСервер")
Процедура КСП_ПартнерПриИзмененииСервер()
	
	КСП_ЗаполнитьСуммуПланаПродаж();
	
КонецПроцедуры

&НаСервере
Процедура КСП_ЗаполнитьСуммуПланаПродаж()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КСП_ПланыПродажСрезПоследних.Коллекция КАК Коллекция,
		|	КСП_ПланыПродажСрезПоследних.Клиент КАК Клиент,
		|	КСП_ПланыПродажСрезПоследних.План КАК План
		|ИЗ
		|	РегистрСведений.КСП_ПланыПродаж.СрезПоследних(
		|			,
		|			Клиент = &Клиент
		|				И Коллекция = &Коллекция) КАК КСП_ПланыПродажСрезПоследних";
	
	Запрос.УстановитьПараметр("Клиент", Объект.Партнер);
	Запрос.УстановитьПараметр("Коллекция", Объект.КСП_Коллекция);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	КСП_ПланПродаж = 0;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		КСП_ПланПродаж = ВыборкаДетальныеЗаписи.План;
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура КСП_ЗаполнитьКоллекцию(Знач Номенклатура)
	
	Объект.КСП_Коллекция = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "КоллекцияНоменклатуры");
	КСП_ЗаполнитьСуммуПланаПродаж();
	
КонецПроцедуры

&НаКлиенте
Процедура КСП_КоллекцияПриИзмененииПосле(Элемент)
	
	КСП_ЗаполнитьСуммуПланаПродаж();

КонецПроцедуры

&НаСервере
&После("КонтрагентПриИзмененииСервер")
Процедура КСП_КонтрагентПриИзмененииСервер()
	
	КСП_ЗаполнитьСуммуПланаПродаж();	
	
КонецПроцедуры

&НаКлиенте
Процедура КСП_ТоварыНоменклатураПриИзмененииПосле(Элемент)

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(Объект.КСП_Коллекция) И ТекущаяСтрока <> Неопределено Тогда
		КСП_ЗаполнитьКоллекцию(ТекущаяСтрока.Номенклатура);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КСП_ОбработкаПроверкиЗаполненияНаСервереПосле(Отказ, ПроверяемыеРеквизиты)
	
	ПроверкаКоллекцииТоваров(Отказ);
	
КонецПроцедуры


&НаСервере
Процедура ПроверкаКоллекцииТоваров(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.КСП_Коллекция) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Номенклатура.Ссылка) КАК Представление,
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.КоллекцияНоменклатуры КАК КоллекцияНоменклатуры
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	НЕ Номенклатура.КоллекцияНоменклатуры В ИЕРАРХИИ (&КоллекцияНоменклатуры)
		|	И Номенклатура.Ссылка В(&СписокНоменклатуры)";
	
	Запрос.УстановитьПараметр("КоллекцияНоменклатуры", Объект.КСП_Коллекция);
	Запрос.УстановитьПараметр("СписокНоменклатуры", Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон("У номенклатуры: ""%1"" не соответствует коллекция по документу", ВыборкаДетальныеЗаписи.Представление),,,, Отказ);
	КонецЦикла;

КонецПроцедуры


#КонецОбласти
