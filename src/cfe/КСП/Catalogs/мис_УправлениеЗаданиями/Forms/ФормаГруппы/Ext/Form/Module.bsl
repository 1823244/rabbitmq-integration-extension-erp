
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СпрОбъект = РеквизитФормыВЗначение("Объект");
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		РасписаниеЗадания = Параметры.ЗначениеКопирования.Расписание.Получить();
		Если ТипЗнч(РасписаниеЗадания) <> Тип("РасписаниеРегламентногоЗадания") Тогда
			//создаем новую переменную типа расписаниеРегламентногоЗаданя
			РасписаниеЗадания = мис_УправлениеЗаданиямиКлиентСервер.ИнициализацияРасписания();
		Иначе
			// проверяем расписание
			Если РасписаниеЗадания.ДниНедели = Неопределено ИЛИ РасписаниеЗадания.ДниНедели.Количество() = 0 Тогда
				Массив = Новый Массив;
				Для ин=1 По 7 Цикл
					Массив.Добавить(ин);
				КонецЦикла;
				РасписаниеЗадания.ДниНедели = Массив;
			КонецЕсли;
		КонецЕсли;

	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Ссылка) И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		// новый
		РасписаниеЗадания = мис_УправлениеЗаданиямиКлиентСервер.ИнициализацияРасписания();
	Иначе
		РасписаниеЗадания = СпрОбъект.Расписание.Получить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ПараметрыЗаписи.Вставить("РасписаниеЗадания" ,РасписаниеЗадания);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.Расписание = Новый ХранилищеЗначения(ПараметрыЗаписи.РасписаниеЗадания,Новый СжатиеДанных(9));
	УстановитьПривилегированныйРежим(Истина);
	мис_РегламентныеЗадания.ПередЗаписьюГруппыСправочникаЗаданий(ТекущийОбъект,ПараметрыЗаписи.РасписаниеЗадания);
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура НадписьОткрытьРасписаниеНажатие(Элемент)
	
	Если ТипЗнч(РасписаниеЗадания) <> Тип("РасписаниеРегламентногоЗадания") Тогда
		//создаем новую переменную типа расписаниеРегламентногоЗаданя
		РасписаниеЗадания = мис_УправлениеЗаданиямиКлиентСервер.ИнициализацияРасписания();
	КонецЕсли;
	
	Асинк = Новый ОписаниеОповещения("Асинк_НадписьОткрытьРасписаниеНажатие", ЭтотОбъект);
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеЗадания);
	Диалог.Показать(Асинк);

КонецПроцедуры

&НаКлиенте
Процедура Асинк_НадписьОткрытьРасписаниеНажатие(Результат, ДопПараметры) Экспорт
	
	Если Результат <> Неопределено  Тогда
		РасписаниеЗадания = Результат;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускПоРасписаниюПриИзменении(Элемент)
	Объект.Активность = Объект.ЗапускПоРасписанию;
КонецПроцедуры
