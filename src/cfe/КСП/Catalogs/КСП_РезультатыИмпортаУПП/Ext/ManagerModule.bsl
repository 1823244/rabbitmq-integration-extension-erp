
// Описание_метода
//
// Параметры:
//	СтруктураПараметры 	- структура - Поля:
//							* ВидДокумента 	- строка, пример "Документ.РеализацияТоваровУслуг"
//							* Номер 		- строка
//							* Дата 			- Дата+Время
//							* ГУИД	 		- Строка, 36
//							* Проведен 		- булево
//							* МассивДокументов - массив созданных в ЕРП документов
//
Процедура ДобавитьЗапись(СтруктураПараметры) Экспорт
	
	Запрос = Новый Запрос("Выбрать спр.ссылка из Справочник.КСП_РезультатыИмпортаУПП как Спр Где ГУИД_УПП = &ГУИД");
	Запрос.УстановитьПараметр("ГУИД", СтруктураПараметры.ГУИД);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОбъектДанных = Справочники.КСП_РезультатыИмпортаУПП.СоздатьЭлемент();
		СсылкаНового = Справочники.КСП_РезультатыИмпортаУПП.ПолучитьСсылку(Новый УникальныйИдентификатор(СтруктураПараметры.ГУИД));
		ОбъектДанных.УстановитьСсылкуНового(СсылкаНового);
		
	Иначе 
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ОбъектДанных = Выборка.Ссылка.ПолучитьОбъект();
	КонецЕсли;
	
	ОбъектДанных.Наименование	 	= СтруктураПараметры.Номер+Строка(СтруктураПараметры.Дата);
	ОбъектДанных.ВидДокументаУПП 	= СтруктураПараметры.ВидДокумента;
	ОбъектДанных.НомерУПП 			= СтруктураПараметры.Номер;
	ОбъектДанных.ДатаУПП 			= СтруктураПараметры.Дата;
	ОбъектДанных.СтатусПроведенияУПП = СтруктураПараметры.Проведен;
	ОбъектДанных.ДатаИмпорта 		= ТекущаяДатаСеанса();
	ОбъектДанных.ГУИД_УПП			= СтруктураПараметры.ГУИД;
	
	ОбъектДанных.ЗагруженныеДокументы.Очистить();
	
	Для каждого Эл Из СтруктураПараметры.МассивДокументов Цикл
		
		НовСтр = ОбъектДанных.ЗагруженныеДокументы.Добавить();
		НовСтр.ДокументЕРП = Эл;
		НовСтр.НомерДокумента = Эл.Номер;
		НовСтр.ДатаДокумента = Эл.Дата;
		
	КонецЦикла;
	
	
	ОбъектДанных.обменданными.загрузка = Истина;
	ОбъектДанных.Записать();
	
		
КонецПроцедуры 


// Создает структуру, для использования в методе ДобавитьЗапись()
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	СтруктураПараметры 	- структура - Поля:
//							* ВидДокумента 	- строка, пример "Документ.РеализацияТоваровУслуг"
//							* Номер 		- строка
//							* Дата 			- Дата+Время
//							* ГУИД	 		- Строка, 36
//							* Проведен 		- булево
//							* МассивДокументов - массив созданных в ЕРП документов
//
//
Функция СтруктураПараметров() Экспорт
	
	СтруктураПараметры 	= Новый Структура;
	СтруктураПараметры.Вставить("ВидДокумента", "");
	СтруктураПараметры.Вставить("Номер", "");
	СтруктураПараметры.Вставить("Дата", '00010101');
	СтруктураПараметры.Вставить("ГУИД", "");
	СтруктураПараметры.Вставить("Проведен", Ложь);
	СтруктураПараметры.Вставить("МассивДокументов", Новый Массив);
		
	Возврат СтруктураПараметры;
	
КонецФункции

